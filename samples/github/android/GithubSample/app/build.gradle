apply plugin: 'com.android.model.application'

model {
    android {
        compileSdkVersion = 22
        buildToolsVersion = "22.0.1"
        sourceCompatibility = 1.7
        targetCompatibility = 1.7

        defaultConfig.with {
            applicationId = "com.google.fletch.githubsample"
            minSdkVersion.apiLevel = 21
            targetSdkVersion.apiLevel = 22
        }
    }

    android.ndk {
        moduleName = "fletch"
        cppFlags += ["-DFLETCH32",
                     "-DFLETCH_TARGET_OS_POSIX",
                     "-DFLETCH_TARGET_OS_LINUX",
                     "-DFLETCH_ENABLE_FFI",
                     "-DFLETCH_ENABLE_LIVE_CODING",
                     "-DANDROID",
                     "-std=c++11"]
        cppFlags += "-I${file("../../../../..")}".toString()
        cppFlags += "-I${file("../../../../../include")}".toString()
        cppFlags += "-I${file("../../../../../third_party/double-conversion/src")}".toString()
        ldLibs += ["android", "dl", "log"]
        stl = "gnustl_static"
    }

    android.sources {
        main {
            java {
                source {
                    srcDir "src/main/java"
                    srcDir "../../../../../package/immi/java"
                }
            }
            jni {
                source {
                    srcDir "src/main/jni"
                }
            }
        }
        arm7 {
            jni {
                source {
                    srcDir "src/arm7/jni"
                }
            }
        }
        x86 {
            jni {
                source {
                    srcDir "src/x86/jni"
                }
            }
        }
    }

    android.buildTypes {
        release {
            minifyEnabled = false
            proguardFiles += file('proguard-rules.pro')
        }
    }

    android.productFlavors {
        create ("x86") {
            ndk.abiFilters += "x86"
            ndk.cppFlags += "-DFLETCH_TARGET_IA32"
        }
        create("arm7") {
            ndk.abiFilters += "armeabi-v7a"
            ndk.cppFlags += "-DFLETCH_TARGET_ARM"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:appcompat-v7:21.0.2'
    compile 'com.android.support:cardview-v7:21.0.+'
    compile 'com.android.support:recyclerview-v7:21.0.+'
}
